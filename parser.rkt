#lang brag
# Mostly adaptated from the offical specification
; https://mlochbaum.github.io/BQN/spec/grammar.html

#()? and [] are equivalent

# @ and / are used to hide nodes
; that aren't needed after parsing
; https://docs.racket-lang.org/brag/#%28part._cuts-and-splices%29

program : [stmt] (/"â‹„" ([stmt] | /nothing))*
@stmt   : expr | def | export
export  : lhs-elt /"â‡"
def     : subDefExpr
        | FuncDefExpr
        | 1M-DefExpr
        | 2M-DefExpr

@expr   : subExpr
        | Func
        | 1Mod
        | 2Mod

2Mod : [atom /"."] 2Mod-custom | @2Mod-literal | 2M-block           | /"(" (@2Mod    | 2M-Expr)  /")"  
1Mod : [atom /"."] 1Mod-custom | @1Mod-literal | 1M-block           | /"(" (@1Mod    | 1M-Expr)  /")"  
Func : [atom /"."] func-custom | @func-literal | FuncBlock          | /"(" (@Func    | FuncExpr) /")"  
atom : [atom /"."] sub-custom  | @sub-literal  | subBlock  | array  | /"(" (@subject | subExpr)  /")"

2M-Def  : 2Mod | /"(" (@2M-Def  | 2M-DefExpr)  /")"
1M-Def  : 1Mod | /"(" (@1M-Def  | 1M-DefExpr)  /")"
FuncDef : Func | /"(" (@FuncDef | FuncDefExpr) /")"
atomDef : atom | /"(" (@subDef  | subDefExpr)  /")" | defArray

array : "âŸ¨" /["â‹„"] [(expr /"â‹„")* expr /["â‹„"]] /"âŸ©"
      | "[" /["â‹„"]  (expr /"â‹„")* expr /["â‹„"]  /"]"

defArray : "âŸ¨" /["â‹„"] [((def | expr) /"â‹„")* (def | expr) /["â‹„"]] /"âŸ©"
         | "[" /["â‹„"]  ((def | expr) /"â‹„")* (def | expr) /["â‹„"]  /"]"

any     : atom | Func | 1Mod | 2Mod
strand  : @any (/"â€¿" @any)+
subject : @atom | strand

anyDef    : atomDef | FuncDef | 1M-Def | 1M-Def
strandDef : @anyDef (/"â€¿" @anyDef)+
subDef    : @atomDef | strandDef

@assign    : "â†" | "â‡" | "â†©"
2M-DefExpr : 2M-Def | @2Mod-custom assign (2M-Expr | 2M-DefExpr) 
1M-DefExpr : 1M-Def | @1Mod-custom assign (1M-Expr | 1M-DefExpr)

2M-Expr : 2Mod | @2Mod-custom "â†©" 2M-Expr
1M-Expr : 1Mod | @1Mod-custom "â†©" 1M-Expr

Derv     : Func |  Operand 1Mod | Operand 2Mod (subject | Func)
@Operand : subject | Derv
Fork     : Derv  | (Operand | /nothing) Derv Fork
Train    : Fork  | Derv Fork
FuncExpr : Train | @func-custom "â†©" FuncExpr

DefDerv     : Derv | FuncDef |  DefOperand 1M-Def | DefOperand 2M-Def (subDef | FuncDef)
@DefOperand : Operand  | subDef  | DefDerv
DefFork     : Fork     | DefDerv | (DefOperand | DefNothing) DefDerv DefFork
DefTrain    : DefFork  | DefDerv DefFork
FuncDefExpr : DefTrain | @func-custom assign (FuncExpr | FuncDefExpr)

arg     : [subject | /nothing] Derv subExpr
nothing : [subject |  nothing] Derv nothing | NOTHING
subExpr : @subject | arg | lhs "â†©" subExpr | lhs Derv "â†©" subExpr

DefArg     : arg     | [subDef | DefNothing] DefDerv subDefExpr
DefNothing : nothing | [subDef | DefNothing] DefDerv DefNothing  | NOTHING
subDefExpr : subDef  |  DefArg | lhs assign subDefExpr | lhs DefDerv "â†©" [subDefExpr]

name      : sub-custom | func-custom | 1Mod-custom | 2Mod-custom
@lhs-sub  : NOTHING | lhsList | lhsArray
lhs-any   : name | lhs-sub | /"(" lhs-elt /")"
lhs-atom  : lhs-any | /"(" lhsStrand /")"
lhs-elt   : lhs-any | lhsStrand
lhs-entry : lhs-elt | lhs "â‡" name
lhsStrand : lhs-atom (/"â€¿" lhs-atom)+
lhsList   : "âŸ¨" /["â‹„"] [(lhs-entry /"â‹„")* lhs-entry /["â‹„"]] "âŸ©"
lhsArray  : "[" /["â‹„"] [(lhs-elt   /"â‹„")* lhs-elt   /["â‹„"]] "]"
lhsComp   : lhs-sub | lhsStrand
@lhs      : sub-custom | lhsComp | /"(" lhs /")"

headW    : lhs | "ğ•¨"
headX    : lhs | "ğ•©"
HeadF    : lhs | func-custom | "ğ•—" | "ğ”½"
HeadG    : lhs | func-custom | "ğ•˜" | "ğ”¾"
2ModLab  : 2Mod-custom | "_ğ•£_"
FuncName : func-custom |  "ğ•Š"
1ModName : [HeadF] (1Mod-custom | "_ğ•£")
2ModName : (2ModLab | HeadF 2ModLab HeadG)

F-head  : FuncName
        | [headW] FuncName      ["â¼"]  headX
        |  headW  FuncName  "Ëœ"  "â¼"   headX
        |         FuncName ["Ëœ"] "â¼"
        | lhsComp

1M-head : 1ModName
        | [headW] 1ModName      ["â¼"] headX
        |  headW  1ModName  "Ëœ"  "â¼"  headX
        | lhsComp

2M-head : 2ModName
        | [headW] 2ModName     ["â¼"] headX
        |  headW  2ModName "Ëœ"  "â¼"  headX
        | lhsComp

body : /["â‹„"] (stmt /"â‹„" | (def | expr) /["â‹„"] "?" /["â‹„"])* stmt /["â‹„"]

 F-case : (/["â‹„"] F-head     /["â‹„"] /":") body
1M-case : (/["â‹„"] 1M-head    /["â‹„"] /":") body
2M-case : (/["â‹„"] 2M-head    /["â‹„"] /":") body
 S-case : (/["â‹„"] sub-custom /["â‹„"] /":") body

FuncBlock : /"{"  F-case (/";" ( F-case | body))* /"}"
          | X-block /FUNC-BLOCK

 1M-block : /"{" 1M-case (/";" (1M-case | body))* /"}"
          | X-block   /1M-BLOCK

 2M-block : /"{" 2M-case (/";" (2M-case | body))* /"}"
          | X-block   /2M-BLOCK

 subBlock : /"{"  S-case (/";" ( S-case | body))* /"}"
          | X-block  /SUB-BLOCK

 @X-block : /"{"    body (/";"            body )*

func-literal : FUNC-PRIM | func-special      | ("â€¢" func-custom)
1Mod-literal : 1MOD-PRIM | "_ğ•£"  | "â¼" | "Ëœ" | ("â€¢" 1Mod-custom)
2Mod-literal : 2MOD-PRIM | "_ğ•£_"             | ("â€¢" 2Mod-custom)
 sub-literal : INTEGER | real | complex 
             | CHARACTER | STRING
             | "â€¢" sub-custom
             | sub-special

func-special : "ğ•" | "ğ•Š" | "ğ•" | "ğ”½" | "ğ”¾"
sub-special  : "ğ•¨" | "ğ•¤" | "ğ•©" | "ğ•£" 

mantissa : "Ï€" | INTEGER [/"." INTEGER]
exponent : ["Â¯"] INTEGER
real     : ["Â¯"] ("âˆ" | mantissa [/("E" | "e") exponent])
complex  : real /("I" | "i") real

#Resolves the janky lexing needed for numerics
sub-custom  : SUB-CUSTOM  |  "e" | "i" 
func-custom : FUNC-CUSTOM |  "E" | "I" 
1Mod-custom : /"_" (@sub-custom | @func-custom)
2Mod-custom : @1Mod-custom /"_"